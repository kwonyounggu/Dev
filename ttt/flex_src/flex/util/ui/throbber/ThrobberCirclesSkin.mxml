<?xml version="1.0" encoding="utf-8"?>
<s:Skin xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:fb="http://ns.adobe.com/flashbuilder/2009"
	alpha.disabled="0.5">
	<fx:Metadata>
		[HostComponent("flex.util.ui.throbber.Throbber")]
	</fx:Metadata>


	<fx:Script fb:purpose="styling">
		<![CDATA[
			public static const COLOR_NORMAL:uint = 0xD6D6D6;

			private static const MAX_ANGLE:Number = (2 * Math.PI);

			private var normalColor:uint = COLOR_NORMAL;

			private var highlightColors:Array = [0x818181, 0x979797, 0xBABABA];

			private const highlightColorsCount:int = 3;

			override protected function updateDisplayList(w:Number, h:Number):void {
				shadow.alpha = (hostComponent.dropShadowVisible ? 0.4 : 0);
				if (isNaN(getStyle("backgroundColor"))) {
					background.visible = false;
				} else {
					background.visible = true;
					bgFill.color = getStyle("backgroundColor");
					bgFill.alpha = getStyle("backgroundAlpha");
				}

				var thickness:int = Math.max(1, int(getStyle("lineThickness")));
				var circleAlpha:Number = getStyle("lineAlpha");
				if (isNaN(circleAlpha)) {
					circleAlpha = 1;
				}

				normalColor = getStyle("color");
				var colorsArray:* = getStyle("highlightColors");
				if (colorsArray is Array) {
					highlightColors = (colorsArray as Array).slice();
				} else {
					var highlightColor:Number = getStyle("highlightColor");
					highlightColors = [];
					if (!isNaN(highlightColor)) {
						highlightColors[0] = highlightColor;
					}
				}
				for (var i:int = highlightColors.length; i < highlightColorsCount; i++) {
					highlightColors[i] = normalColor;
				}

				drawCircles(w, h, thickness, circleAlpha);

				super.updateDisplayList(w, h);
			}

			protected function drawCircles(w:Number, h:Number, thickness:Number = 5, circleAlpha:Number = 1):void {
				var g:Graphics = throbber.graphics;
				g.clear();
				g.lineStyle(0, 0, 0); // no border
				var midX:int = Math.round(w / 2);
				var midY:int = Math.round(h / 2);
				var radius:int = Math.min(midX, midY) - Math.ceil(thickness / 2);

				if ((radius > 0) && (thickness > 0) && (circleAlpha > 0)) {
					var angle:Number = 0;
					var incr:Number = MAX_ANGLE / hostComponent.maxSteps;
					var smallRadius:int = Math.round(radius / 10);
					var count:int = 0;
					while (angle < MAX_ANGLE) {
						var color:uint = getColor(count);
						var circleRadius:int = getCircleRadius(count, smallRadius);

						// figure out the position around the circle
						var x1:Number = midX + (radius * Math.sin(angle));
						var y1:Number = midY - (radius * Math.cos(angle));
						drawCircle(g, color, x1, y1, circleRadius, circleAlpha);

						angle += incr;
						count++;
					}

				}
			}

			/**
			 * Draws a small circle at the given position.
			 */
			protected function drawCircle(g:Graphics, color:uint, midX:Number, midY:Number, radius:Number, circleAlpha:Number = 1):void {
				if ((radius > 0) && (circleAlpha > 0)) {
					g.beginFill(color, circleAlpha);
					g.drawCircle(midX, midY, radius);
					g.endFill();
				}
			}

			private function getColor(step:int):uint {
				var color:uint = normalColor;
				if (hostComponent.currentStep >= 0) {
					var diff:int = (hostComponent.currentStep - step);
					if (diff < 0) {
						diff += hostComponent.maxSteps;
					}
					if (diff == 0) {
						color = highlightColors[0];
					} else if (diff == 1) {
						color = highlightColors[1];
					} else if (diff == 2) {
						color = highlightColors[2];
					} else {
						color = normalColor;
					}
				}
				return color;
			}

			private function getCircleRadius(count:int, baseRadius:int):int {
				var circleRadius:int = baseRadius;
				if (hostComponent.currentStep >= 0) {
					var diff:int = (hostComponent.currentStep - count);
					if (diff < 0) {
						diff += hostComponent.maxSteps;
					}
					if (diff == 0) {
						circleRadius += 3;
					} else if (diff == 1) {
						circleRadius += 2;
					} else if (diff == 2) {
						circleRadius += 1;
					}
				}
				return circleRadius;
			}
		]]>
	</fx:Script>

	<s:states>
		<s:State name="normal"/>
		<s:State name="disabled"/>
	</s:states>

	<!--- Defines the background. -->
	<s:Rect id="background"
		bottom="0"
		left="0"
		right="0"
		top="0">
		<s:fill>
			<s:SolidColor id="bgFill"
				color="#E6E6E6"/>
		</s:fill>
	</s:Rect>

	<mx:UIComponent id="throbber"
		bottom="2"
		left="2"
		right="2"
		top="2">
		<mx:filters>
			<s:DropShadowFilter id="shadow"
				color="#000000"
				alpha="0.4"
				angle="90"
				distance="2"/>
		</mx:filters>
	</mx:UIComponent>

</s:Skin>
